#include <WiFi.h>
#include "mywifi.h"
#include <ESPmDNS.h>
#include "led.h"
#include <ArduinoJson.h>
#include <LittleFS.h>
#include <ESPAsyncWebServer.h>

unsigned long lastReconnectAttempt = 0;
AsyncWebServer server(80);

bool isWiFiSet() {
  return LittleFS.exists("/wifi.json");
}

void WIFiConfig::startWiFiOrAP() {
  settings = loadWiFiSettings();
  if (!connectToWiFi() && !settings.connectedOnce) {
    Serial.println("‚ùå Nem siker√ºlt csatlakozni a Wi-Fi-hez, ind√≠t√°s AP m√≥dban...");
    if (!settings.connectedOnce) Serial.println("‚ùå... mert m√©g eddig nem csatlakozott arra a h√°l√≥zatra.");
    startAccessPoint();
  } 
  startMDNS();
}

void WIFiConfig::resetButtonHandle(int pressDuration) {
  if (pressDuration > 3000 && !apMode) { // 3 m√°sodperc
      Serial.println("üÜò Gomb hossz√∫ lenyom√°s ‚Äì AP m√≥d aktiv√°l√°sa!");
      
      WiFi.disconnect(true);
      delay(500);
      apMode = true;
      resetwifi = true;
      startAccessPoint();
    }
}

WifiSettings WIFiConfig::loadWiFiSettings() {
  WifiSettings settings = {"", "", false};

  if (!LittleFS.begin()) {
    Serial.println("‚ùå LittleFS inicializ√°l√°sa sikertelen.");
    return settings;
  }

  File file = LittleFS.open("/wifi.json", "r");
  if (!file) {
    Serial.println("‚ùå Nem tal√°lhat√≥ a wifi.json f√°jl.");
    return settings;
  }

  JsonDocument doc;
  DeserializationError error = deserializeJson(doc, file);
  if (error) {
    Serial.println("‚ùå Hiba a JSON beolvas√°sa k√∂zben:");
    Serial.println(error.c_str());
    return settings;
  }

  settings.ssid = doc["ssid"].as<String>();
  settings.password = doc["password"].as<String>();
  settings.connectedOnce = doc["connectedOnce"];
  Serial.println("üì• Wi-Fi be√°ll√≠t√°sok bet√∂ltve:");
  Serial.println("SSID: " + settings.ssid);
  // Ne √≠rd ki a jelsz√≥t biztons√°gi okb√≥l
  file.close();
  return settings;
}

// Wi-Fi adatok ment√©se wifi.json-be
void WIFiConfig::saveWiFiSettings(bool connectedOnce) {
  JsonDocument doc;
  doc["ssid"] = settings.ssid;
  doc["password"] = settings.password;
  doc["connectedOnce"] = connectedOnce;

  File file = LittleFS.open("/wifi.json", "w");
  if (file) {
    serializeJson(doc, file);
    file.close();
  }
}

bool WIFiConfig::connectToWiFi() {
  if (settings.ssid == "" || settings.password == "") {
    Serial.println("‚ùå Hi√°nyz√≥ Wi-Fi adatok.");
    return false;
  }

  WiFi.mode(WIFI_STA);
  WiFi.begin(settings.ssid.c_str(), settings.password.c_str());

  Serial.print("üîå Csatlakoz√°s a Wi-Fi-hez... ");

  for (int i = 0; i < 20; i++) {
    if (WiFi.status() == WL_CONNECTED) {
      Serial.println("‚úÖ Csatlakozva: " + WiFi.localIP().toString());
      ledSetColor(false, true, false);
      if (!settings.connectedOnce) {
        settings.connectedOnce = true;
        saveWiFiSettings(true);
      }
      apMode = false;
      return true;
    }
    delay(500);
    Serial.print(".");
  }

  Serial.println("\n‚ùå Sikertelen kapcsol√≥d√°s.");
  ledSetColor(true, false, false);
  return false;
}

void WIFiConfig::reconnectIfNeeded() {
  if (!resetwifi && settings.connectedOnce) {
    if (WiFi.status() != WL_CONNECTED && millis() - lastReconnectAttempt > reconnectInterval) {
      Serial.println("üîÅ √öjrapr√≥b√°lkoz√°s Wi-Fi csatlakoz√°sra...");
      lost_connection = true;
      WiFi.begin(settings.ssid.c_str(), settings.password.c_str());
      lastReconnectAttempt = millis();
    }
    if (lost_connection && WiFi.status() == WL_CONNECTED) {
      Serial.println("‚úÖ Csatlakozva: " + WiFi.localIP().toString());
      ledSetColor(false, true, false);
      if (!settings.connectedOnce) {
        saveWiFiSettings(true);
      }
      lost_connection = false;
      apMode = false;
    }
  }
}

void WIFiConfig::startAccessPoint() {
  Serial.println("AP m√≥d aktiv√°l√°sa: vilagitas-setup");
  WiFi.mode(WIFI_AP);
  WiFi.softAP(configSsid);
  Serial.println("üåê AP m√≥d: http://" + WiFi.softAPIP().toString());
  apMode = true;
  ledSetColor(false, false, true); // K√©k = AP m√≥d
}

void WIFiConfig::startMDNS() {
  if (MDNS.begin(dnsName)) Serial.println("üåç mDNS: http://"+String(dnsName)+".local");
  else Serial.println("‚ùå mDNS hiba");
}

void WIFiConfig::setupWebServer() {
  server.on("/", HTTP_GET, [this](AsyncWebServerRequest *request) {
    if (!this->apMode && this->settings.connectedOnce && !this->resetwifi) {
      Serial.println("üåê Wi-Fi be√°ll√≠tva, kiszolg√°l√≥ ind√≠t√°sa...");
      request->send(LittleFS, "/index.html", "text/html");
    } else {
      request->send(LittleFS, "/wifi.html", "text/html");
    }
  });

  server.on("/savewifi", HTTP_POST, [this](AsyncWebServerRequest *request) {
    if (this->apMode && (!this->settings.connectedOnce || this->resetwifi)) {
      if (request->hasParam("ssid", true) && request->hasParam("password", true)) {
        String ssid = request->getParam("ssid", true)->value();
        String password = request->getParam("password", true)->value();
        
        this->resetwifi = false;
        settings = {ssid, password, false}; // Reset the connectedOnce flag
        saveWiFiSettings(false); // Save the new settings without marking as connectedOnce
        request->send(200, "text/plain; charset=utf-8", "‚úÖ Mentve, pr√≥b√°l csatlakozni...");

        ledSetColor(false, false, true);
        delay(500);
        ESP.restart();
      } else {
        request->send(400, "text/plain; charset=utf-8", "Hi√°nyz√≥ mez≈ë.");
      }
    } else {
      request->send(500, "text/plain; charset=utf-8", "Az oldal nem tal√°lhat√≥ (legal√°bb is ha m√°r be van √°ll√≠tva a WiFi).");
    }
  });

  server.begin();
}
